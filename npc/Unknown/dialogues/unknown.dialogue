~ unknown_dialogue

set dialogue_is_going = true

if relationship == "unknown":
	Незнакомец: Здравствуй.
	Незнакомец: Хоть мы и впервые видимся, я хотел бы тебя кое о чём попросить.
	Незнакомец: Я совсем никак не могу открыть этот сундучок, ведь рук у меня совсем нет.
	Незнакомец: А когда я пытаюсь прикоснуться к чему-то своим телом, то прохожу сквозь.
	Незнакомец: Не мог бы ты открыть его для меня и достать от туда лежащую там вещь?
	- Конечно, помогу.
		set relationship = "helper"
		Незнакомец: Благодарю, буду ждать тебя прямо здесь.
	- Нет, решай свои проблемы сам.
		set relationship = "selfish"
		Незнакомец: Тогда не смею задерживать...

elif relationship == "helper":
	if strange_item == "has":
		Незнакомец: О, вижу она у тебя. Можешь надеть её на меня?
		- Что это за цепочка?
			Незнакомец: Один добрый человек сделал его для меня, чтобы окружающий меня мир вновь стал осязаемым.
			Незнакомец: Но есть один злой человек, который стащил с меня эту цепочку, и вот уже несколько дней как я живу как призрак.
			- Хорошо, наклонись немного.
				set relationship = "friend"
				Незнакомец: Спасибо тебе огромное, ты очень добрый... человек?
				Незнакомец: В принципе, это не так уж и важно, ха-ха!
			- Нет, я оставлю это у себя.
				set relationship = "enemy"
				Незнакомец: Вот значит как? Ну ты ещё пожалеешь об этом.
				Незнакомец: Сгинь, видеть тебя не желаю.
	elif asked == false:
		Незнакомец: Что такое?
		- Для сундучка не требуется ключ?
			set asked = true
			Незнакомец: Нет, тот идиот не закрыл его на ключ, так что, отчасти, нам повезло.
			Незнакомец: Так что можешь идти и спокойно открыть его.
	else:
		Незнакомец: Почему-бы тебе не пойти и не взять вещь из сундучка?

elif relationship == "selfish":
	if strange_item == "has":
		Незнакомец: Ты всё-таки решил мне помочь?
		- Что это за цепочка?
			Незнакомец: Один добрый человек сделал его для меня, чтобы окружающий меня мир вновь стал осязаемым.
			Незнакомец: Но есть один злой человек, который стащил с меня эту цепочку, и вот уже несколько дней как я живу как призрак.
			- Хорошо, наклонись немного.
				set relationship = "friend"
				Незнакомец: Спасибо тебе огромное, ты очень добрый... человек?
				Незнакомец: В принципе, это не так важно, ха-ха!
			- Нет, я оставлю это у себя.
				set relationship = "enemy"
				Незнакомец: Вот значит как? Ну ты ещё пожалеешь об этом.
				Незнакомец: Проваливай, видеть тебя не желаю.
	elif asked == false:
		Незнакомец: Что такое? Подумал над ответом и передумал?
		- Для сундучка не требуется ключ?
			set asked = true
			Незнакомец: Нет, тот идиот не закрыл его на ключ, так что, отчасти, нам повезло.
			Незнакомец: Так что можешь идти и спокойно открыть его.
		- Нет, просто подошёл посмотреть.
			Незнакомец: Не на что тут смотреть, проваливай.
	else:
		Незнакомец: Почему-бы тебе не пойти и не взять вещь из сундучка?

elif relationship == "enemy":
	Незнакомец: Чего подошёл? Передумал?
	- Да, наклонись немного.
		set relationship = "friend"
		Незнакомец: Спасибо тебе огромное, ты очень добрый... человек?
		Незнакомец: В принципе, это не так уж и важно, ха-ха!
	- Нет, просто поиздеваться подошёл.
		Незнакомец: ...

elif relationship == "friend":
	Незнакомец: Здравствуй, друг. Огромное тебе спасибо за вещицу. Я уже думал что весь свой остаток жизни проживу вот так... Хотя я и так мёртв, ха-ха-ха!
	if buy_a_item == true and otodvigator == false:
		Незнакомец: Кажется ты хотел кое-что спросить
		- Ты до сих пор продаёшь ОТОДВИГАТОР3128?
			set otodvigator = true
			Незнакомец: Ох, ты пришёл за этим? Извини что напряг тебя со своим делом.
			Незнакомец: Хотя, в любом случае пришлось бы достать эту вещь.
			Незнакомец: Вот, держи, отдаю тебе его бесплатно, услуга за услугу.
			[[ПОЛУЧЕН ПРЕДМЕТ ОТОДВИГАТОР3128]]
		- Нет, ничего.
			Незнакомец: Тогда, желаю удачи. Приходи в любое время.
	elif otodvigator == true:
		Незнакомец: Надеюсь моя вещица тебе пригодилась! Приходи за покупками ещё.

set dialogue_is_going = false

=> END


~ get_item

set dialogue_is_going = true

if (relationship == "helper" or relationship == "selfish" or relationship == "enemy" or relationship == "friend"):
	if strange_item == "no":
		Вы открываете сундук. В сундуке обнаруживается тёмная цепочка.
		- Взять цепочку.
			[В ВАШ ИНВЕНТАРЬ ДОБАВЛЕН ПРЕДМЕТ Тёмная цепочка.]
			set strange_item = "has"
			if relationship == "helper":
				Вероятно, вы захотите отнести цепочку тому призраку.
			else:
				Вероятно, вы захотите оставить эту цепочку себе.
		- Закрыть сундук.
			Вы решили не трогать эту вещь. Возможно она наложит на вас проклятие?
	else:
		В этом сундуке больше ничего нет.
else:
	Кажется, этот призрак охраняет этот сундук. Попробуйте с ним поговорить.

set dialogue_is_going = false

=> END